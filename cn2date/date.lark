start: date 

date: years _year_delimiter months _month_delimiter days _day_delimiter?
    | years _year_delimiter months _month_delimiter?
    | months _month_delimiter days _day_delimiter?
    | years _year_delimiter?
    | months _month_delimiter?
    | days _day_delimiter?

years : _year_2
      | _year_4
months: _month_1_9
      | _month_01_09
      | _month_10_12
days  : _day_1_9
      | _day_01_09
      | _day_10_29
      | _day_30_31

_year_delimiter : "年"
                | _delimiter
_month_delimiter: "月"
                | _delimiter
_day_delimiter  : "日"
                | "号"
_delimiter      : "-"
                | "/"

_year_4: ONE_NINE ZERO_NINE ZERO_NINE ZERO_NINE
_year_2: ZERO_NINE ZERO_NINE

_month_10_12: /[1一]/ /[0-2零一二]/
            | /十/ /[12一二]/?
_month_01_09: /[0零]/ _month_1_9
_month_1_9  : ONE_NINE

_day_30_31: /[3三]/ (/[01零一]/ | /十/ /[1一]/?)
_day_10_29: /[12一二]/ ZERO_NINE
          | /[2二]/? /十/
          | /[2二]/? /十/ ONE_NINE
_day_01_09: /[0零]/ _day_1_9
_day_1_9  : ONE_NINE

ZERO_NINE: /[0-9零一二三四五六七八九]/
ONE_NINE : /[1-9一二三四五六七八九]/

===

start: nl

nl       : FRONT "个"? DATE_UNIT
         | FRONT _nl_digit+ "个"? DATE_UNIT
         | _nl_digit "个"? DATE_UNIT REAR?
_nl_digit: DIGIT | NL_DIGIT

FRONT      : "今" | "本" | "当前" | "这个" | "明" | "昨" | "去" | "前" | "后" | "上半" | "下半" | "上" | "下"
REAR       : NEXT_WORD | "前" | "后" | "内" | "以内" | "之内"
DATE_UNIT  : "年" | "季度" | "月" | "周" | "星期" | "天" | "日" | "午"
NEXT_WORD  : "以来" | "以前" | "以后" | "之前" | "之后"
NL_DIGIT   : "两"
DIGIT      : /["0-9零一二三四五六七八九十"]/

// 忽略的字符
%ignore " " | "份" | "度" | "第"